
init_kickmaster = [
    kickmaster_cn = $currentmaster
    kickmaster_votes = 0
    foreach (players) [player_var $arg1 kickmaster_voted 0]
]

get_votes_needed = [
    if (> $playercount 2) [(- (+ (div (- $playercount 1) 2) 1) $kickmaster_votes)] [(- 2 $kickmaster_votes)]
]

check_kickmaster = [
    if (= (get_votes_needed) 0) [
        msg (info (format "#kickmaster vote passed, kicking and banning %1 from server." (player_name $currentmaster)))
        kick $currentmaster
        init_kickmaster -1
    ]
]

if (&& (!= $currentmaster -1) [strcmp (player_priv $currentmaster) master]) [

    if (! (symbol? kickmaster_installed_handlers)) [
        kickmaster_cn = -1
        kickmaster_votes = 0
        kickmaster_installed_handlers = 1
        event_handler $ondisconnect [
            parameters cn
            if (&& (!= $kickmaster_cn -1) [= (player_var $cn kickmaster_voted) 1]) [
                player_var $cn kickmaster_voted 0
                kickmaster_votes = (- $kickmaster_votes 1)
                check_kickmaster
            ]
        ]
        event_handler $onsetmaster [if (= $currentmaster -1) [init_kickmaster -1]]
    ]
    
    if (= $kickmaster_cn -1) [init_kickmaster $currentmaster]
    
    if (= (player_var $cn kickmaster_voted) 1) [
        privmsg $cn (err "You've already voted to kick the master.")
    ] [
        player_var $cn kickmaster_voted 1
        kickmaster_votes = (+ $kickmaster_votes 1)
        foreach (players) [
            if (!= $arg1 $currentmaster) [
                privmsg $arg1 (format "%1 wants the master kicked from the server, if you agree type #kickmaster." (player_name $cn))
                privmsg $arg1 (info (format "%1 more votes needed to kick master." (get_votes_needed)))
            ]
        ]
        check_kickmaster
    ]
] [
    privmsg $cn (err "Nobody is master.")
]
